plugins {
    id 'nebula.java-cross-compile' version '0.10.0' apply false
    id 'com.github.johnrengelman.shadow' version '2.0.4' apply false
    id 'me.tatarka.gradle.pom' version '1.0' apply false
    id 'net.researchgate.release' version '2.7.0' apply false
}

if (project.hasProperty('teamcity')) {
    println "##teamcity[buildNumber '$version (#${project.'build.number'})']"
    println "##teamcity[setParameter name='javacord.version' value='$version']"
}

def rootBuildDir = buildDir

ext {
    releaseVersion = !version.toString().endsWith('-SNAPSHOT')
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'nebula.java-cross-compile'
    apply plugin: 'checkstyle'

    group = 'org.javacord'
    description = 'An easy to use multithreaded library for creating Discord bots in Java'
    buildDir = rootProject.file("${rootBuildDir}/${name}")

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding 'UTF-8'
        options.incremental true
    }

    checkstyle {
        toolVersion '8.9'
        maxWarnings 0
    }
}

clean {
    delete rootBuildDir
}

subprojects {
    ext.shortName = name - ~/^javacord-/
    description = "An easy to use multithreaded library for creating Discord bots in Java ($shortName)"
}

defaultTasks 'build'

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/jars.gradle'
apply from: 'gradle/java9.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/publishing.gradle'
