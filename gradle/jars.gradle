subprojects {
    apply plugin: 'osgi'

    task javadocJar(type: Jar) {
        group 'build'
        description 'Assembles a jar archive containing the JavaDoc files.'
        classifier 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        group 'build'
        description 'Assembles a jar archive containing the Java source files.'
        classifier 'sources'
        from sourceSets.main.allJava
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

project(':javacord-api') {
    jar {
        manifest {
            instruction 'Export-Package', '!org.javacord.*.internal.*', "*;version=$version;-noimport:=true"
        }
    }
}

project(':javacord-core') {
    jar {
        manifest {
            instruction 'Fragment-Host', "org.javacord.api;bundle-version=\"[$version,$version]\""
            instruction 'Import-Package', '!org.javacord.api.*', '*'
            instruction 'Import-Package', 'org.slf4j.impl;resolution:=optional', '*'
            instruction 'Export-Package', ''
        }
    }
}

// remove the main JAR from the archives artifacts
configurations.archives.artifacts.clear()

apply plugin: 'com.github.johnrengelman.shadow'
// remove non-sense task from com.github.johnrengelman.shadow plugin
tasks.remove knows

shadowJar {
    classifier 'shaded'
}

artifacts {
    archives shadowJar
}

// disable and remove the jar task
jar {
    enabled false
}
tasks.remove jar
