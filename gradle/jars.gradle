subprojects {
    apply plugin: 'biz.aQute.bnd.builder'

    task javadocJar(type: Jar) {
        group 'build'
        description 'Assembles a jar archive containing the JavaDoc files.'
        classifier 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        group 'build'
        description 'Assembles a jar archive containing the Java source files.'
        classifier 'sources'
        from sourceSets.main.allJava
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    artifacts {
        archives javadocJar, sourcesJar
    }
}

project(':javacord-api') {
    jar {
        bnd ('Export-Package': "!org.javacord.*.internal.*|*;version=${archiveVersion.get().replace("-", ".")};-noimport:=true",
             '-fixupmessages': '^Classes found in the wrong directory: \\\\{META-INF/versions/9/module-info\\.class=module-info}$')
    }
}

project(':javacord-core') {
    jar {
        bnd ('Fragment-Host': "org.javacord.api;bundle-version=${archiveVersion.get().replace("-", ".")}",
             'Import-Package': '!org.javacord.api.*|*',
             'Export-Package': '',
             '-fixupmessages': '^Classes found in the wrong directory: \\\\{META-INF/versions/9/module-info\\.class=module-info}$')
    }
}

// remove the main JAR from the archives artifacts
configurations.archives.artifacts.clear()

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    classifier 'shaded'
}

artifacts {
    archives shadowJar
}

// disable the jar task but do not remove it,
// this is not nicely compatible with composite builds
jar {
    enabled false
}
